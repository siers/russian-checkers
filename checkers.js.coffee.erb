BLACK   = false
WHITE   = true
REGULAR = false
QUEEN   = true

# This is a mixin, and rules /logically/ actually extend piece, not vice versa.
class Rules
  # Checks whether on a black square only.
  @black = (cons) ->
    [x, y] = cons ? @target
    (x + y) % 2 == 1
  black: @black

  canAttack: ->
    false

  didAttack: ->
    true

  # Checks whether can attack and if so, if it's being done.
  attack: ->
    return true unless @canAttack()
    return true if @didAttack()

  valid: (cons) ->
    @target = cons
    return false unless @black()
    return false unless @attack()
    return true

class Piece extends Rules
  constructor: (@cons, @color, @set) ->
    @element()

  selected: (e) =>
    e.stopPropagation()
    @element.toggleClass('selected')

  piece = $("<div class='piece' />")

  element: ->
    @element = piece.
      clone(true).
      appendTo(Checkers.field).
      bind('click', @selected)

  classify: ->
    klass = ['piece']
    klass.push "p#{ "hgfedcba"[@cons[0]] }"
    klass.push "p#{ @cons[1]+1 }"
    klass.push "#{ if @color == WHITE then 'white' else 'black' }"
    @element.attr('class', klass.join(' '))

  render: ->
    @classify()

class Pieces
  X = 0
  Y = 1
  array: []
  constructor: ->
    for x in [0..7]
      @array.push([])
      for y in [0..7]
        _(@array).last().push(null)

  new: (args...) ->
    [[x, y], _...] = args
    @array[x][y] = new Piece(args..., this)

  get: (cons) ->
    @array[cons[X]][cons[Y]]

  each: (fun) ->
    _.chain(@array).flatten().compact().each(fun)

class Moves
  positions: [] # board version list

  constructor: ->
    @positions.push @pieces

class Board extends Moves
  pieces: new Pieces
  unit: null # pixels per one square

  constructor: ->
    @create()
    super

  create: ->
    for x in [0..7]
      for y in [0..5]
        if Rules.black([x, y])
          y += 2 if y > 2
          @pieces.new([x, y], y > 3 ? WHITE : BLACK)

  release: (e) => # Release can be anywhere.
    x    = Math.floor(e.originalEvent.layerX / @unit)
    y    = Math.floor(e.originalEvent.layerY / @unit)
    cons = [x, y]
    sel  = found = @find()
    @put(sel, cons) if found and sel.valid(cons)

  put: (piece, cons) -> # Put only where cons are valid.
    piece.cons = cons
    piece.render()

  find: (fun = null) ->
    fun = fun || (p) -> p.element.is('.selected')
    ret = undefined

    @pieces.each (p) ->
      return ((ret = p) && false) if fun(p)

    ret

  render: ->
    @pieces.each (p) -> p.render()

Game.list.checkers = Checkers =
  init: (@field) ->
    @board = new Board
    @board.unit = @field.width() / 8
    @board.render()
    @field.addClass('checkers').bind('click', @board.release)

  stop: ->
    null
